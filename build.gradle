plugins {
    id 'fabric-loom' version '0.4.3'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version "1.1.2"
    id "com.modrinth.minotaur" version "1.1.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

def ENV = System.getenv()
version = version + "+" + (ENV.BUILD_NUMBER ? ("build." + ENV.BUILD_NUMBER) : "custom")

minecraft {
}

repositories {
    maven {
        name "Hexeption Mod Repo"
        url "https://repo.hexeption.dev/repository/hex-mods/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
    modCompile "io.github.prospector:modmenu:${project.mod_menu_version}"
    modCompile "uk.co.hexeption:DevWorld_1.15:1.15.2+1.0.0+build.9"

    modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
        if (project.hasProperty("mavenPass")) {
            maven {
                url = "https://repo.hexeption.dev/repository/hex-mods/"
                credentials {
                    username = "admin"
                    password = project.getProperty("mavenPass")
                }
            }
        }
    }
}

curseforge {
    if (project.hasProperty('curse_api_key')) {
        apiKey = project.getProperty('curse_api_key')
    }
    project {
        id = '383801'
        changelog = 'A changelog can be found at https://github.com/Hexeption/LightBlock'
        releaseType = 'beta'
        addGameVersion '1.15'
        mainArtifact(remapJar) {
            displayName = "LightBlock v$version for $project.minecraft_version"
        }
        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){
    if (project.hasProperty('modrinth_api_key')) {
        token = project.getProperty('modrinth_api_key')
    }
    projectId = 'RAYYJHp3'
    versionName = "LightBlock v$version for $project.minecraft_version"
    changelog = 'A changelog can be found at https://github.com/Hexeption/LightBlock'
    versionNumber = "$version"
    uploadFile = remapJar
    addGameVersion("$minecraft_version")
    addLoader('fabric')
}

